plugins {
    id "architectury-plugin" version "3.4+"
    id "dev.architectury.loom" version "1.4.+" apply false
    id "org.sonarqube" version "4.0.+"
    id "com.teamresourceful.resourcefulgradle"
}

architectury {
    minecraft = mc_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$mc_version"
        mappings loom.officialMojangMappings()

        modApi "earth.terrarium.botarium:botarium-$name-1.20.1:$botarium_version"
    }
}

sonar {
    properties {
        property "sonar.projectKey", "Resourceful-Bees_ResourcefulBees"
        property "sonar.organization", "resourceful-bees"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = libs.versions.mod.version.get() //resourcefulGradle.modVersion.getModVersion(project)
    group = maven_group

    repositories {
        maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }
        mavenCentral()
        maven { url = "https://www.cursemaven.com" }
        mavenLocal()
        maven { url = "https://jitpack.io" }
        maven { url = 'https://api.modrinth.com/maven/' }
        maven { url = 'https://maven.ladysnake.org/releases' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }

    publishing {
        if (project != rootProject) {
            publications {
                maven(MavenPublication) {
                    artifactId = "$archives_base_name-$project.name-$mc_version"
                    from components.java

                    pom {
                        name = 'Resourceful Bees'
                        description = 'A mod for minecraft to add own bees to minecraft through a powerful api.'
                        url = 'https://github.com/Team-Resourceful/ResourcefulBees'

                        scm {
                            connection = 'git:https://github.com/Team-Resourceful/ResourcefulBees.git'
                            developerConnection = 'git:https://github.com/Team-Resourceful/ResourcefulBees.git'
                            url = 'https://github.com/Team-Resourceful/ResourcefulBees/'
                        }

                        licenses {
                            license {
                                name = 'GNU Lesser General Public License v3.0'
                                url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id = 'epic428'
                                name = 'Epic_Oreo'
                                email = 'admin@resourcefulbees.com'
                            }
                            developer {
                                id = 'thatgravyboat'
                                name = 'ThatGravyBoat'
                                email = 'admin@resourcefulbees.com'
                            }
                            developer {
                                id = 'dawn_felstar'
                                name = 'Dawn Felstar'
                                email = 'admin@resourcefulbees.com'
                            }
                        }
                    }
                }
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            maven {
                url = nightly.toBoolean()
                        ? "https://nexus.resourcefulbees.com/repository/maven-nightly/"
                        : "https://nexus.resourcefulbees.com/repository/maven-releases/"
                credentials {
                    def ENV = System.getenv()
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASS
                }
            }
        }
    }
}


resourcefulGradle {
    templates {
        readme {
            source = file("dev_resources/project_templates/README.md.template")
            injectedValues = [
                    "version"            : libs.versions.mod.version.get(),
                    "mc_version"         : mc_version,
                    "phos_version"       : phos_version,
                    "gecko_forge"        : geckolib_forge,
                    "gecko_forge_version": geckolib_version,
                    "rlib_version"       : rlib_version,
                    "rconfig_version"    : rconfig_version
            ]
        }
        discordEmbed {
            injectedValues = [
                    "version"      : libs.versions.mod.version.get(),
                    "mc_version"   : mc_version,
                    "forge_version": forge_version
            ]

            def template = nightly.toBoolean() ? "nightly_embed.json.template" : "release_embed.json.template"
            source = file("dev_resources/project_templates/$template")
        }
    }
}
